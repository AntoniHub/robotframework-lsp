// To test: gradlew clean test
// To build: gradlew buildPlugin
// Check all tasks: gradlew tasks

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
}

group 'robotframework.intellij'
version '0.11.0'

task finishBuild(type: Exec) {
    def finishArgs = ['python', "finish_build.py"]
    commandLine finishArgs
}

buildPlugin.finalizedBy finishBuild

repositories {
    mavenCentral()

    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 11
    targetCompatibility = 11
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    compile group: 'org.eclipse.lsp4j', name: 'org.eclipse.lsp4j', version: '0.11.0-SNAPSHOT'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2020.3'
}

patchPluginXml {
    changeNotes """
    <p>
    Release 0.11.0
    </p>
    <ul>
      <li>[Intellij] Improved initialization of language server to validate the python executable and ask for a new one if the one found is not valid.</li>
      <li>[Intellij] Fix PYTHONPATH so that the language server shipped in the Intellij plugin is the one used.</li>
      <li>[Intellij] When requesting a completion, cancel previous one requested.</li>
      <li>[Intellij] Language server executable can be project dependent.</li>
      <li>[Robocop](https://robocop.readthedocs.io/en/latest/) is now used to lint (enabled by default, can be disabled in settings).</li>
      <li>Snippets completion is now properly case-insensitive.</li>
      <li>If there's some problem computing semantic tokens, log it and still return a valid value.</li>
      <li>If a keyword name is defined in the context, don't try to create an auto-import for it.</li>
      <li>Reworded some settings to be clearer.</li>
    </ul>    
    <br>
    Current feature set:<br>
    <ul>
    <li>Settings page for the language server</li>
    <li>Code completion</li>
    <li>Code analysis</li>
    <li>Go to definition</li>
    <li>Browse Keywords (symbols)</li>
    <li>Syntax highlighting (with `semanticTokens/full` request)</li>
    </ul>
    <br>
    <strong>
    Important: The Intellij Language Server integration is currently in alpha for early access.<br>
    Please report any issues found during testing at:<br>
    <br>
    <a href="https://github.com/robocorp/robotframework-lsp/issues">https://github.com/robocorp/robotframework-lsp/issues</a>.
    </strong>
      """
}

test {
    useJUnit()

    systemProperty 'NO_FS_ROOTS_ACCESS_CHECK', true
    systemProperty 'idea.ProcessCanceledException', 'disabled'

    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
        showStandardStreams true
    }
}