// To test: gradlew clean test
// To build: gradlew buildPlugin
// Check all tasks: gradlew tasks

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
}

group 'robotframework.intellij'
version '0.8.0'

task finishBuild(type: Exec) {
    def finishArgs = ['python', "finish_build.py"]
    commandLine finishArgs
}

buildPlugin.finalizedBy finishBuild

repositories {
    mavenCentral()
}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 11
    targetCompatibility = 11
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    compile group: 'org.eclipse.lsp4j', name: 'org.eclipse.lsp4j', version: '0.10.0'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2020.3'
}

patchPluginXml {
    changeNotes """
    Initial release with support for:<br>
    <ul>
    <li>Settings page for the language server</li>
    <li>Code completion</li>
    <li>Code analysis</li>
    <li>Go to definition</li>
    <li>Browse Keywords (symbols)</li>
    <li>Syntax highlighting (very basic right now)</li>
    </ul>
    <br>
    <strong>
    Important: The Intellij Language Server integration is currently in alpha for early access.<br>
    Please report any issues found during testing at:<br>
    <br>
    <a href="https://github.com/robocorp/robotframework-lsp/issues">https://github.com/robocorp/robotframework-lsp/issues</a>.
    </strong>
      """
}

test {
    useJUnit()

    systemProperty 'NO_FS_ROOTS_ACCESS_CHECK', true
    systemProperty 'idea.ProcessCanceledException', 'disabled'

    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
        showStandardStreams true
    }
}